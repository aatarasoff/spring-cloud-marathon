apply plugin: 'nebula.maven-publish'
apply plugin: 'nebula.compile-api'
apply plugin: 'nebula.source-jar'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'nebula.integtest-standalone'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compileOnly "org.springframework.boot:spring-boot-starter-web"
    compileOnly "org.springframework.boot:spring-boot-starter-actuator"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    compileOnly "org.springframework.cloud:spring-cloud-commons"
    compileOnly "org.springframework.cloud:spring-cloud-netflix-core"
    compileOnly "org.springframework.cloud:spring-cloud-starter-netflix-ribbon"
    compileOnly "org.springframework.cloud:spring-cloud-starter-openfeign"

    compileOnly "com.mesosphere:marathon-client"
    compileOnly "com.netflix.feign:feign-ribbon"

    //FIXME: sourceSets fails on NoClassDef exception
    integTestCompile "org.springframework.boot:spring-boot-starter-web"
    integTestCompile "org.springframework.boot:spring-boot-starter-actuator"
    integTestCompile "org.springframework.boot:spring-boot-configuration-processor"
    integTestCompile "org.springframework.cloud:spring-cloud-commons"
    integTestCompile "org.springframework.cloud:spring-cloud-netflix-core"
    integTestCompile "org.springframework.cloud:spring-cloud-starter-netflix-ribbon"
    integTestCompile "org.springframework.cloud:spring-cloud-starter-openfeign"

    integTestCompile "com.mesosphere:marathon-client"
    integTestCompile "com.netflix.feign:feign-ribbon"
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

bintray {
    user = System.properties['BINTRAY_USER']
    key = System.properties['BINTRAY_KEY']
    publications = ['nebula']
    publish = true
    pkg {
        repo = 'maven'
        name = 'spring-cloud-marathon-autoconfigure'
    }
}

task integrationTestsSetup(type: Exec) {
    workingDir "${rootProject.projectDir}"
    commandLine 'docker-compose', 'up', '-d'
    doLast {
        sleep 30000
    }
}

task integrationTestsDeploy(type: Exec) {
    workingDir "${rootProject.projectDir}"
    commandLine "bash", "-c", "curl -XPOST http://localhost:8080/v2/apps?force=true -v " +
            "-H \"Content-Type: application/json\" " +
            "--data-binary @test-marathon-app-manifest.json"
    doLast {
        sleep 30000
    }
}

task integrationTestsTeardown(type: Exec) {
    workingDir "${rootProject.projectDir}"
    commandLine "bash", "-c", "docker-compose stop && docker-compose rm -f"
}

integrationTestsDeploy.dependsOn(integrationTestsSetup)
integrationTest.dependsOn(integrationTestsDeploy)
integrationTest.finalizedBy(integrationTestsTeardown)


sourceSets {
    test.compileClasspath += configurations.compileOnly
    test.runtimeClasspath += configurations.compileOnly
}
