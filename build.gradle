buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.1.RELEASE'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.2.0'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:5.1.0'
        classpath 'com.netflix.nebula:nebula-project-plugin:3.2.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}


allprojects {
    group = 'info.developerblog.spring.cloud'
    version = '0.0.1'

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Finchley.SR1"
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.16.8'
            dependency 'com.mesosphere:marathon-client:0.6.0'
            dependency 'com.netflix.feign:feign-ribbon:8.14.1'
            dependency 'org.unitils:unitils-core:3.4.2'
            dependency 'com.jayway.jsonpath:json-path:2.2.0'
        }
    }

    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testCompile ('org.unitils:unitils-core') {
            //exclude this cause it has conflict with slf4j
            exclude group: 'commons-logging'
        }
        testCompile 'com.jayway.jsonpath:json-path'
    }
}
