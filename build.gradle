buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE")
        classpath 'com.netflix.nebula:nebula-project-plugin:3.2.1'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
        classpath 'com.netflix.nebula:nebula-release-plugin:4.0.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}


allprojects {
    group = 'info.developerblog.spring.cloud'
    version = '0.0.1'

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven {
            url "https://downloads.mesosphere.com/maven/"
        }
    }

    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'provided-base'
    apply plugin: 'optional-base'
    apply plugin: 'nebula.nebula-release'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Camden.SR6"
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.16.8'
            dependency 'com.mesosphere:marathon-client:0.3.0'
            dependency 'com.netflix.feign:feign-ribbon:8.14.1'
            dependency 'org.mockito:mockito-all:1.10.19'
            dependency 'org.unitils:unitils-core:3.4.2'
            dependency 'com.jayway.jsonpath:json-path:2.2.0'
        }
    }

    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    dependencies {
        provided 'org.projectlombok:lombok'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testCompile 'org.mockito:mockito-all'
        testCompile 'org.unitils:unitils-core'
        testCompile 'com.jayway.jsonpath:json-path'
    }
}
